// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.1"

        classpath 'com.google.gms:google-services:4.3.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile file("../config/checkstyle/checkstyle.xml")

        source 'src/main/java'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'
        ignoreFailures = true
        // empty classpath
        classpath = files()

        reports {
            xml.enabled true
            xml {
                destination file("../reports/checkstyle/checkstyle-report.xml") // this is okay
            }
            html.enabled true
            html {
                destination file("../reports/checkstyle/checkstyle-report.html")
            }
        }
    }

    task pmd(type: Pmd) {
        consoleOutput = true
        rulePriority = 5
        ruleSets = ["category/java/errorprone.xml", "category/java/bestpractices.xml"]

        source 'src/main/java'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'
        ignoreFailures = true
        // empty classpath
        classpath = files()

        reports {
            xml.enabled true
            xml {
                destination file("../reports/pmd/pmd-report.xml") // this is okay
            }
            html.enabled true
            html {
                destination file("../reports/pmd/pmd-report.html")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}